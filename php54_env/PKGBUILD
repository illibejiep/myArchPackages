# $Id$
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgbase=php
pkgname=(
	'php5.4.32_env'
	'php5.4.32_env-cgi'
	'php5.4.32_env-apache'
	'php5.4.32_env-fpm'
	'php5.4.32_env-embed'
	'php5.4.32_env-pear'
	'php5.4.32_env-enchant'
	'php5.4.32_env-gd'
	'php5.4.32_env-intl'
	'php5.4.32_env-ldap'
	'php5.4.32_env-mcrypt'
	'php5.4.32_env-mssql'
	'php5.4.32_env-odbc'
	'php5.4.32_env-pgsql'
	'php5.4.32_env-pspell'
	'php5.4.32_env-snmp'
	'php5.4.32_env-sqlite'
	'php5.4.32_env-tidy'
	'php5.4.32_env-xsl'
)
pkgver=5.4.32
PHPENV="/tmp/php${pkgver}_env"
install='php5.4.32_env.install'
pkgrel=2
arch=('i686' 'x86_64')
license=('PHP')
url='http://www.php.net'
makedepends=('apache' 'imap' 'postgresql-libs' 'libldap' 'postfix' 'libvpx'
             'sqlite' 'unixodbc' 'net-snmp' 'libzip' 'enchant' 'file' 'freetds'
             'libmcrypt' 'tidyhtml' 'aspell' 'libltdl' 'libpng' 'libjpeg' 'icu'
             'curl' 'libxslt' 'openssl' 'bzip2' 'db' 'gmp' 'freetype2' 'systemd')
source=(
	"${pkgbase}-${pkgver}.tar.gz::http://ru2.php.net/get/${pkgbase}-${pkgver}.tar.gz/from/this/mirror"
	'php.ini.patch' 
	'apache.conf' 
	'php-fpm.conf.in.patch'
    'php-fpm'
)
md5sums=(
	'f0bd6e6cf4ea39dd5ef5c5b7e640b49b'
	'e8be02ecdb45ec259c48374d17680efb'
	'dec2cbaad64e3abf4f0ec70e1de4e8e9'
	'594fddc363831767815c4ae6130bf09e'
	'13634a8dbc4b6f01318c80411c3a3cbc'
)
prepare() {
	cd ${srcdir}/${pkgbase}-${pkgver}

	patch -p0 -i ${srcdir}/php.ini.patch
	patch -p0 -i ${srcdir}/php-fpm.conf.in.patch
}
build() {
	local _phpconfig="--srcdir=../${pkgbase}-${pkgver} \
		--config-cache \
		--prefix=$PHPENV \
		--sbindir=$PHPENV/usr/bin \
		--sysconfdir=$PHPENV/etc/php \
		--localstatedir=$PHPENV/var \
		--with-layout=GNU \
		--with-config-file-path=$PHPENV/etc/php \
		--with-config-file-scan-dir=$PHPENV/etc/php/conf.d \
		--disable-rpath \
		--mandir=$PHPENV/usr/share/man \
		--without-pear \
		"

	local _phpextensions="--enable-bcmath=shared \
		--enable-calendar=shared \
		--enable-dba=shared \
		--enable-exif=shared \
		--enable-ftp=shared \
		--enable-gd-native-ttf \
		--enable-intl=shared \
		--enable-mbstring \
		--enable-phar=shared \
		--enable-posix=shared \
		--enable-shmop=shared \
		--enable-soap=shared \
		--enable-sockets=shared \
		--enable-sysvmsg=shared \
		--enable-sysvsem=shared \
		--enable-sysvshm=shared \
		--enable-zip=shared \
		--with-bz2=shared \
		--with-curl=shared \
		--with-db4=/usr \
		--with-enchant=shared,/usr \
		--with-fpm-systemd \
		--with-freetype-dir=/usr \
		--with-gd=shared \
		--with-gdbm \
		--with-gettext=shared \
		--with-gmp=shared \
		--with-iconv=shared \
		--with-icu-dir=/usr \
		--with-imap-ssl \
		--with-imap=shared \
		--with-jpeg-dir=/usr \
		--with-vpx-dir=/usr \
		--with-ldap=shared \
		--with-ldap-sasl \
		--with-mcrypt=shared \
		--with-mhash \
		--with-mssql=shared \
		--with-mysql-sock=/var/run/mysqld/mysqld.sock \
		--with-mysql=shared,mysqlnd \
		--with-mysqli=shared,mysqlnd \
		--with-openssl=shared \
		--with-pcre-regex=/usr \
		--with-pdo-mysql=shared,mysqlnd \
		--with-pdo-odbc=shared,unixODBC,/usr \
		--with-pdo-pgsql=shared \
		--with-pdo-sqlite=shared,/usr \
		--with-pgsql=shared \
		--with-png-dir=/usr \
		--with-pspell=shared \
		--with-snmp=shared \
		--with-sqlite3=shared,/usr \
		--with-tidy=shared \
		--with-unixODBC=shared,/usr \
		--with-xmlrpc=shared \
		--with-xsl=shared \
		--with-zlib \
		"

	EXTENSION_DIR=${PHPENV}/usr/lib/php/modules
	export EXTENSION_DIR
	PEAR_INSTALLDIR=${PHPENV}/usr/share/pear
	export PEAR_INSTALLDIR

	cd ${srcdir}/${pkgbase}-${pkgver}

	# php
	mkdir ${srcdir}/build-php
	cd ${srcdir}/build-php
	ln -s ../${pkgbase}-${pkgver}/configure
	./configure ${_phpconfig} \
		--disable-cgi \
		--with-readline \
		--enable-pcntl \
		${_phpextensions}
	make

	# cgi and fcgi
	# reuse the previous run; this will save us a lot of time
	cp -a ${srcdir}/build-php ${srcdir}/build-cgi
	cd ${srcdir}/build-cgi
	./configure ${_phpconfig} \
		--disable-cli \
		--enable-cgi \
		${_phpextensions}
	make

	# apache
	cp -a ${srcdir}/build-php ${srcdir}/build-apache
	cd ${srcdir}/build-apache
	make clean
	./configure ${_phpconfig} \
		--disable-cli \
		--with-apxs2 \
		${_phpextensions}
	make

	# fpm
	cp -a ${srcdir}/build-php ${srcdir}/build-fpm
	cd ${srcdir}/build-fpm
	./configure ${_phpconfig} \
		--disable-cli \
		--enable-fpm \
		--with-fpm-user=http \
		--with-fpm-group=http \
		${_phpextensions}
	make

	# embed
	cp -a ${srcdir}/build-php ${srcdir}/build-embed
	cd ${srcdir}/build-embed
	./configure ${_phpconfig} \
		--disable-cli \
		--enable-embed=shared \
		${_phpextensions}
	make

	# pear
	cp -a ${srcdir}/build-php ${srcdir}/build-pear
	cd ${srcdir}/build-pear
	./configure ${_phpconfig} \
		--disable-cgi \
		--with-readline \
		--enable-pcntl \
		--with-pear \
		${_phpextensions}
	make
}

# check() {
# 	cd ${srcdir}/build-php
# 	export SNMP_TIMEOUT=1
# 	export SNMP_RETRIES=0
# 	make test
# }

package_php5.4.32_env() {
	pkgdesc='An HTML-embedded scripting language'
	depends=('phpenv' 'pcre' 'libxml2' 'bzip2' 'curl')

	install -d -m1777 ${pkgdir}/tmp	
	
	cd ${srcdir}/build-php
	make -j1 INSTALL_ROOT=${pkgdir} install
	install -d -m755 ${pkgdir}${PHPENV}/usr/share/pear
	# install php.ini
	install -D -m644 ${srcdir}/${pkgbase}-${pkgver}/php.ini-production ${pkgdir}${PHPENV}/etc/php/php.ini
	install -d -m755 ${pkgdir}${PHPENV}/etc/php/conf.d/

	# remove static modules
	rm -f ${pkgdir}${PHPENV}/usr/lib/php/modules/*.a
	# remove modules provided by sub packages
	rm -f ${pkgdir}${PHPENV}/usr/lib/php/modules/{enchant,gd,intl,ldap,mcrypt,mssql,odbc,pdo_odbc,pgsql,pdo_pgsql,pspell,snmp,sqlite3,pdo_sqlite,tidy,xsl}.so
	# remove empty directory
	rm -rf ${pkgdir}${PHPENV}/usr/include/php/include
}

package_php5.4.32_env-cgi() {
	pkgdesc='CGI and FCGI SAPI for PHP'
	depends=('php5.4.32_env')
	install -d -m1777 ${pkgdir}/tmp	
	install -D -m755 ${srcdir}/build-cgi/sapi/cgi/php-cgi ${pkgdir}${PHPENV}/usr/bin/php-cgi
}

package_php5.4.32_env-apache() {
	pkgdesc='Apache SAPI for PHP'
	depends=('php5.4.32_env' 'apache')
	install -d -m1777 ${pkgdir}/tmp	
	install -D -m755 ${srcdir}/build-apache/libs/libphp5.so ${pkgdir}${PHPENV}/usr/lib/httpd/modules/libphp5.so
	install -D -m644 ${srcdir}/apache.conf ${pkgdir}${PHPENV}/etc/httpd/conf/extra/php5_module.conf
}

package_php5.4.32_env-fpm() {
	pkgdesc='FastCGI Process Manager for PHP'
	depends=('php5.4.32_env' 'systemd')

	install -d -m1777 ${pkgdir}/tmp	
	install -D -m755 ${srcdir}/php-fpm ${pkgdir}${PHPENV}/bin/php-fpm
	install -D -m755 ${srcdir}/build-fpm/sapi/fpm/php-fpm ${pkgdir}${PHPENV}/usr/sbin/php-fpm_env

	install -D -m644 ${srcdir}/build-fpm/sapi/fpm/php-fpm.8 ${pkgdir}${PHPENV}/usr/share/man/man8/php-fpm.8
	install -D -m644 ${srcdir}/build-fpm/sapi/fpm/php-fpm.conf ${pkgdir}${PHPENV}/etc/php/php-fpm.conf
	install -d -m755 ${pkgdir}${PHPENV}/etc/php/fpm.d
	install -d -m755 ${pkgdir}${PHPENV}/var/log
	install -d -m755 ${pkgdir}${PHPENV}/run
}

package_php5.4.32_env-embed() {
	pkgdesc='Embed SAPI for PHP'
	depends=('php5.4.32_env')
	install -d -m1777 ${pkgdir}/tmp	
	install -D -m755 ${srcdir}/build-embed/libs/libphp5.so ${pkgdir}${PHPENV}/usr/lib/libphp5.so
	install -D -m644 ${srcdir}/${pkgbase}-${pkgver}/sapi/embed/php_embed.h ${pkgdir}${PHPENV}/usr/include/php/sapi/embed/php_embed.h
}

package_php5.4.32_env-pear() {
	pkgdesc='PHP Extension and Application Repository'
	depends=('php5.4.32_env')
	install -d -m1777 ${pkgdir}/tmp	
	cd ${srcdir}/build-pear
	make install-pear INSTALL_ROOT=${pkgdir}
	rm -rf ${pkgdir}/.{channels,depdb,depdblock,filemap,lock,registry}
	rm -rf ${pkgdir}${PHPENV}/share/pear/.{channels,depdb,depdblock,filemap,lock,registry}
}

package_php5.4.32_env-enchant() {
	depends=('php5.4.32_env' 'enchant')
	pkgdesc='enchant module for PHP'
	install -d -m1777 ${pkgdir}/tmp	
	install -D -m755 ${srcdir}/build-php/modules/enchant.so ${pkgdir}${PHPENV}/usr/lib/php/modules/enchant.so
}

package_php5.4.32_env-gd() {
	depends=('php5.4.32_env' 'libpng' 'libjpeg' 'freetype2' 'libvpx')
	pkgdesc='gd module for PHP'
	install -d -m1777 ${pkgdir}/tmp	
	install -D -m755 ${srcdir}/build-php/modules/gd.so ${pkgdir}${PHPENV}/usr/lib/php/modules/gd.so
}

package_php5.4.32_env-intl() {
	depends=('php5.4.32_env' 'icu')
	pkgdesc='intl module for PHP'
	install -d -m1777 ${pkgdir}/tmp	
	install -D -m755 ${srcdir}/build-php/modules/intl.so ${pkgdir}${PHPENV}/usr/lib/php/modules/intl.so
}

package_php5.4.32_env-ldap() {
	depends=('php5.4.32_env' 'libldap')
	pkgdesc='ldap module for PHP'
	install -d -m1777 ${pkgdir}/tmp	
	install -D -m755 ${srcdir}/build-php/modules/ldap.so ${pkgdir}${PHPENV}/usr/lib/php/modules/ldap.so
}

package_php5.4.32_env-mcrypt() {
	depends=('php5.4.32_env' 'libmcrypt' 'libltdl')
	pkgdesc='mcrypt module for PHP'
	install -d -m1777 ${pkgdir}/tmp	
	install -D -m755 ${srcdir}/build-php/modules/mcrypt.so ${pkgdir}${PHPENV}/usr/lib/php/modules/mcrypt.so
}

package_php5.4.32_env-mssql() {
	depends=('php5.4.32_env' 'freetds')
	pkgdesc='mssql module for PHP'
	install -d -m1777 ${pkgdir}/tmp	
	install -D -m755 ${srcdir}/build-php/modules/mssql.so ${pkgdir}${PHPENV}/usr/lib/php/modules/mssql.so
}

package_php5.4.32_env-odbc() {
	depends=('php5.4.32_env' 'unixodbc')
	pkgdesc='ODBC modules for PHP'
	install -d -m1777 ${pkgdir}/tmp	
	install -D -m755 ${srcdir}/build-php/modules/odbc.so ${pkgdir}${PHPENV}/usr/lib/php/modules/odbc.so
	install -D -m755 ${srcdir}/build-php/modules/pdo_odbc.so ${pkgdir}${PHPENV}/usr/lib/php/modules/pdo_odbc.so
}

package_php5.4.32_env-pgsql() {
	depends=('php5.4.32_env' 'postgresql-libs')
	pkgdesc='PostgreSQL modules for PHP'
	install -d -m1777 ${pkgdir}/tmp	
	install -D -m755 ${srcdir}/build-php/modules/pgsql.so ${pkgdir}${PHPENV}/usr/lib/php/modules/pgsql.so
	install -D -m755 ${srcdir}/build-php/modules/pdo_pgsql.so ${pkgdir}${PHPENV}/usr/lib/php/modules/pdo_pgsql.so
}

package_php5.4.32_env-pspell() {
	depends=('php5.4.32_env' 'aspell')
	pkgdesc='pspell module for PHP'
	install -d -m1777 ${pkgdir}/tmp	
	install -D -m755 ${srcdir}/build-php/modules/pspell.so ${pkgdir}${PHPENV}/usr/lib/php/modules/pspell.so
}

package_php5.4.32_env-snmp() {
	depends=('php5.4.32_env' 'net-snmp')
	pkgdesc='snmp module for PHP'
	install -d -m1777 ${pkgdir}/tmp	
	install -D -m755 ${srcdir}/build-php/modules/snmp.so ${pkgdir}${PHPENV}/usr/lib/php/modules/snmp.so
}

package_php5.4.32_env-sqlite() {
	depends=('php5.4.32_env' 'sqlite')
	pkgdesc='sqlite module for PHP'
	install -d -m1777 ${pkgdir}/tmp	
	install -D -m755 ${srcdir}/build-php/modules/sqlite3.so ${pkgdir}${PHPENV}/usr/lib/php/modules/sqlite3.so
	install -D -m755 ${srcdir}/build-php/modules/pdo_sqlite.so ${pkgdir}${PHPENV}/usr/lib/php/modules/pdo_sqlite.so
}

package_php5.4.32_env-tidy() {
	depends=('php5.4.32_env' 'tidyhtml')
	pkgdesc='tidy module for PHP'
	install -d -m1777 ${pkgdir}/tmp	
	install -D -m755 ${srcdir}/build-php/modules/tidy.so ${pkgdir}${PHPENV}/usr/lib/php/modules/tidy.so
}

package_php5.4.32_env-xsl() {
	depends=('php5.4.32_env' 'libxslt')
	pkgdesc='xsl module for PHP'
	install -d -m1777 ${pkgdir}/tmp	
	install -D -m755 ${srcdir}/build-php/modules/xsl.so ${pkgdir}${PHPENV}/usr/lib/php/modules/xsl.so
}
