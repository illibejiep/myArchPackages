# $Id$
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

PHPENV="${HOME}/.phpenv/versions/5.5.9"

pkgbase=php
pkgname=('php55'
         'php55-cgi'
         'php55-apache'
         'php55-fpm'
         'php55-embed'
         'php55-pear'
         'php55-enchant'
         'php55-gd'
         'php55-intl'
         'php55-ldap'
         'php55-mcrypt'
         'php55-mssql'
         'php55-odbc'
         'php55-pgsql'
         'php55-pspell'
         'php55-snmp'
         'php55-sqlite'
         'php55-tidy'
         'php55-xsl')
pkgver=5.5.9
pkgrel=1
arch=('i686' 'x86_64')
license=('PHP')
url='http://www.php.net'
makedepends=('apache' 'imap' 'postgresql-libs' 'libldap' 'postfix' 'libvpx'
             'sqlite' 'unixodbc' 'net-snmp' 'libzip' 'enchant' 'file' 'freetds'
             'libmcrypt' 'tidyhtml' 'aspell' 'libltdl' 'libpng' 'libjpeg' 'icu'
             'curl' 'libxslt' 'openssl' 'bzip2' 'db' 'gmp' 'freetype2' 'systemd')
source=("http://www.php.net/distributions/${pkgbase}-${pkgver}.tar.gz"
        "http://www.php.net/distributions/${pkgbase}-${pkgver}.tar.gz.asc"
        'php.ini.patch' 'apache.conf' 'php-fpm.conf.in.patch'
        'logrotate.d.php-fpm' 'php-fpm.service' 'php-fpm.tmpfiles')
md5sums=('c018461604db38af48d1ca304cb592c6'
         'SKIP'
         '799cd5f91f5253f4b47ec4fd7fccf4f1'
         'dec2cbaad64e3abf4f0ec70e1de4e8e9'
         '9f8263996836e27f5be1483e86ac2e33'
         'e036c721e5ad927cd1613c7475a58b3a'
         'cc2940f5312ba42e7aa1ddfab74b84c4'
         'c60343df74f8e1afb13b084d5c0e47ed')

prepare() {
	cd ${srcdir}/${pkgbase}-${pkgver}

	patch -p0 -i ${srcdir}/php.ini.patch
	patch -p0 -i ${srcdir}/php-fpm.conf.in.patch
}

build() {
	local _phpconfig="--srcdir=../${pkgbase}-${pkgver} \
		--prefix=$PHPENV \
		--sbindir=$PHPENV/bin \
		--sysconfdir=$PHPENV/etc \
		--localstatedir=$PHPENV/var \
		--with-layout=GNU \
		--with-config-file-path=$PHPENV/etc \
		--with-config-file-scan-dir=$PHPENV/etc/conf.d \
		--disable-rpath \
		--mandir=$PHPENV/share/man \
		--without-pear \
		"

	local _phpextensions="--enable-bcmath=shared \
		--enable-calendar=shared \
		--enable-dba=shared \
		--enable-exif=shared \
		--enable-ftp=shared \
		--enable-gd-native-ttf \
		--enable-intl=shared \
		--enable-mbstring \
		--enable-opcache \
		--enable-phar=shared \
		--enable-posix=shared \
		--enable-shmop=shared \
		--enable-soap=shared \
		--enable-sockets=shared \
		--enable-sysvmsg=shared \
		--enable-sysvsem=shared \
		--enable-sysvshm=shared \
		--enable-zip=shared \
		--with-bz2=shared \
		--with-curl=shared \
		--with-db4=/usr \
		--with-enchant=shared,/usr \
		--with-fpm-systemd \
		--with-freetype-dir=/usr \
		--with-gd=shared \
		--with-gdbm \
		--with-gettext=shared \
		--with-gmp=shared \
		--with-iconv=shared \
		--with-icu-dir=/usr \
		--with-imap-ssl \
		--with-imap=shared \
		--with-jpeg-dir=/usr \
		--with-vpx-dir=/usr \
		--with-ldap=shared \
		--with-ldap-sasl \
		--with-mcrypt=shared \
		--with-mhash \
		--with-mssql=shared \
		--with-mysql-sock=/run/mysqld/mysqld.sock \
		--with-mysql=shared,mysqlnd \
		--with-mysqli=shared,mysqlnd \
		--with-openssl=shared \
		--with-pcre-regex=/usr \
		--with-pdo-mysql=shared,mysqlnd \
		--with-pdo-odbc=shared,unixODBC,/usr \
		--with-pdo-pgsql=shared \
		--with-pdo-sqlite=shared,/usr \
		--with-pgsql=shared \
		--with-png-dir=/usr \
		--with-pspell=shared \
		--with-snmp=shared \
		--with-sqlite3=shared,/usr \
		--with-tidy=shared \
		--with-unixODBC=shared,/usr \
		--with-xmlrpc=shared \
		--with-xsl=shared \
		--with-zlib \
		"

	EXTENSION_DIR=$PHPENV/lib/php/modules
	export EXTENSION_DIR
	PEAR_INSTALLDIR=$PHPENV/share/pear
	export PEAR_INSTALLDIR

	cd ${srcdir}/${pkgbase}-${pkgver}

	# php
	mkdir ${srcdir}/build-php
	cd ${srcdir}/build-php
	ln -s ../${pkgbase}-${pkgver}/configure
	./configure ${_phpconfig} \
		--disable-cgi \
		--with-readline \
		--enable-pcntl \
		${_phpextensions}
	make

	# cgi and fcgi
	# reuse the previous run; this will save us a lot of time
	cp -a ${srcdir}/build-php ${srcdir}/build-cgi
	cd ${srcdir}/build-cgi
	./configure ${_phpconfig} \
		--disable-cli \
		--enable-cgi \
		${_phpextensions}
	make

	# apache
	cp -a ${srcdir}/build-php ${srcdir}/build-apache
	cd ${srcdir}/build-apache
	./configure ${_phpconfig} \
		--disable-cli \
		--with-apxs2 \
		${_phpextensions}
	make

	# fpm
	cp -a ${srcdir}/build-php ${srcdir}/build-fpm
	cd ${srcdir}/build-fpm
	./configure ${_phpconfig} \
		--disable-cli \
		--enable-fpm \
		--with-fpm-user=http \
		--with-fpm-group=http \
		${_phpextensions}
	make

	# embed
	cp -a ${srcdir}/build-php ${srcdir}/build-embed
	cd ${srcdir}/build-embed
	./configure ${_phpconfig} \
		--disable-cli \
		--enable-embed=shared \
		${_phpextensions}
	make

	# pear
	cp -a ${srcdir}/build-php ${srcdir}/build-pear
	cd ${srcdir}/build-pear
	./configure ${_phpconfig} \
		--disable-cgi \
		--with-readline \
		--enable-pcntl \
		--with-pear=$PHPENV/share/pear \
		${_phpextensions}
	make
}

#check() {
# 	cd ${srcdir}/build-php
# 	export SNMP_TIMEOUT=1
# 	export SNMP_RETRIES=0
# 	printf "n\n" | make test
# 	echo
#}

package_php55() {
	pkgdesc='An HTML-embedded scripting language'
	depends=('pcre' 'libxml2' 'bzip2' 'curl')
	backup=('${PHPENV}/etc/php.ini')

	cd ${srcdir}/build-php
	make -j1 INSTALL_ROOT=${pkgdir} install
	install -d -m755 ${pkgdir}${PHPENV}/share/pear
	# install php.ini
	install -D -m644 ${srcdir}/${pkgbase}-${pkgver}/php.ini-production ${pkgdir}${PHPENV}/etc/php.ini
	install -d -m755 ${pkgdir}${PHPENV}/etc/conf.d/

	# remove static modules
	rm -f ${pkgdir}${PHPENV}/lib/php/modules/*.a
	# remove modules provided by sub packages
	rm -f ${pkgdir}${PHPENV}/lib/php/modules/{enchant,gd,intl,ldap,mcrypt,mssql,odbc,pdo_odbc,pgsql,pdo_pgsql,pspell,snmp,sqlite3,pdo_sqlite,tidy,xsl}.so
	# remove empty directory
	rmdir ${pkgdir}${PHPENV}/include/php/include
}

package_php55-cgi() {
	pkgdesc='CGI and FCGI SAPI for PHP'
	depends=('php55')

	install -D -m755 ${srcdir}/build-cgi/sapi/cgi/php-cgi ${pkgdir}${PHPENV}/bin/php-cgi
}

package_php55-apache() {
	pkgdesc='Apache SAPI for PHP'
	depends=('php55' 'apache')
	backup=('${PHPENV}/etc/httpd/conf/extra/php5_module.conf')

	install -D -m755 ${srcdir}/build-apache/libs/libphp5.so ${pkgdir}${PHPENV}/lib/httpd/modules/libphp5.so
	install -D -m644 ${srcdir}/apache.conf ${pkgdir}${PHPENV}/etc/httpd/conf/extra/php5_module.conf
}

package_php55-fpm() {
	pkgdesc='FastCGI Process Manager for PHP'
	depends=('php55' 'systemd')
	backup=('${PHPENV}/etc/php-fpm.conf')
	install='php-fpm.install'

	install -D -m755 ${srcdir}/build-fpm/sapi/fpm/php-fpm ${pkgdir}${PHPENV}/bin/php-fpm
	install -D -m644 ${srcdir}/build-fpm/sapi/fpm/php-fpm.8 ${pkgdir}${PHPENV}/share/man/man8/php-fpm.8
	install -D -m644 ${srcdir}/build-fpm/sapi/fpm/php-fpm.conf ${pkgdir}${PHPENV}/etc/php-fpm.conf
	install -D -m644 ${srcdir}/logrotate.d.php-fpm ${pkgdir}${PHPENV}/etc/logrotate.d/php-fpm
	install -d -m755 ${pkgdir}${PHPENV}/etc/fpm.d
	install -D -m644 ${srcdir}/php-fpm.tmpfiles ${pkgdir}${PHPENV}/lib/tmpfiles.d/php-fpm.conf
	install -D -m644 ${srcdir}/php-fpm.service ${pkgdir}${PHPENV}/lib/systemd/system/php-fpm.service
}

package_php55-embed() {
	pkgdesc='Embed SAPI for PHP'
	depends=('php55')

	install -D -m755 ${srcdir}/build-embed/libs/libphp5.so ${pkgdir}${PHPENV}/lib/libphp5.so
	install -D -m644 ${srcdir}/${pkgbase}-${pkgver}/sapi/embed/php_embed.h ${pkgdir}${PHPENV}/include/php/sapi/embed/php_embed.h
}

package_php55-pear() {
	pkgdesc='PHP Extension and Application Repository'
	depends=('php55')
	backup=('${PHPENV}/etc/pear.conf')

	cd ${srcdir}/build-pear
	make install-pear INSTALL_ROOT=${pkgdir}
	rm -rf ${pkgdir}${PHPENV}/.{channels,depdb,depdblock,filemap,lock,registry}
}

package_php55-enchant() {
	depends=('php55' 'enchant')
	pkgdesc='enchant module for PHP'
	install -D -m755 ${srcdir}/build-php/modules/enchant.so ${pkgdir}${PHPENV}/lib/php/modules/enchant.so
}

package_php55-gd() {
	depends=('php55' 'libpng' 'libjpeg' 'freetype2' 'libvpx')
	pkgdesc='gd module for PHP'
	install -D -m755 ${srcdir}/build-php/modules/gd.so ${pkgdir}${PHPENV}/lib/php/modules/gd.so
}

package_php55-intl() {
	depends=('php55' 'icu')
	pkgdesc='intl module for PHP'
	install -D -m755 ${srcdir}/build-php/modules/intl.so ${pkgdir}${PHPENV}/lib/php/modules/intl.so
}

package_php55-ldap() {
	depends=('php55' 'libldap')
	pkgdesc='ldap module for PHP'
	install -D -m755 ${srcdir}/build-php/modules/ldap.so ${pkgdir}${PHPENV}/lib/php/modules/ldap.so
}

package_php55-mcrypt() {
	depends=('php55' 'libmcrypt' 'libltdl')
	pkgdesc='mcrypt module for PHP'
	install -D -m755 ${srcdir}/build-php/modules/mcrypt.so ${pkgdir}${PHPENV}/lib/php/modules/mcrypt.so
}

package_php55-mssql() {
	depends=('php55' 'freetds')
	pkgdesc='mssql module for PHP'
	install -D -m755 ${srcdir}/build-php/modules/mssql.so ${pkgdir}${PHPENV}/lib/php/modules/mssql.so
}

package_php55-odbc() {
	depends=('php55' 'unixodbc')
	pkgdesc='ODBC modules for PHP'
	install -D -m755 ${srcdir}/build-php/modules/odbc.so ${pkgdir}${PHPENV}/lib/php/modules/odbc.so
	install -D -m755 ${srcdir}/build-php/modules/pdo_odbc.so ${pkgdir}${PHPENV}/lib/php/modules/pdo_odbc.so
}

package_php55-pgsql() {
	depends=('php55' 'postgresql-libs')
	pkgdesc='PostgreSQL modules for PHP'
	install -D -m755 ${srcdir}/build-php/modules/pgsql.so ${pkgdir}${PHPENV}/lib/php/modules/pgsql.so
	install -D -m755 ${srcdir}/build-php/modules/pdo_pgsql.so ${pkgdir}${PHPENV}/lib/php/modules/pdo_pgsql.so
}

package_php55-pspell() {
	depends=('php55' 'aspell')
	pkgdesc='pspell module for PHP'
	install -D -m755 ${srcdir}/build-php/modules/pspell.so ${pkgdir}${PHPENV}/lib/php/modules/pspell.so
}

package_php55-snmp() {
	depends=('php55' 'net-snmp')
	pkgdesc='snmp module for PHP'
	install -D -m755 ${srcdir}/build-php/modules/snmp.so ${pkgdir}${PHPENV}/lib/php/modules/snmp.so
}

package_php55-sqlite() {
	depends=('php55' 'sqlite')
	pkgdesc='sqlite module for PHP'
	install -D -m755 ${srcdir}/build-php/modules/sqlite3.so ${pkgdir}${PHPENV}/lib/php/modules/sqlite3.so
	install -D -m755 ${srcdir}/build-php/modules/pdo_sqlite.so ${pkgdir}${PHPENV}/lib/php/modules/pdo_sqlite.so
}

package_php55-tidy() {
	depends=('php55' 'tidyhtml')
	pkgdesc='tidy module for PHP'
	install -D -m755 ${srcdir}/build-php/modules/tidy.so ${pkgdir}${PHPENV}/lib/php/modules/tidy.so
}

package_php55-xsl() {
	depends=('php55' 'libxslt')
	pkgdesc='xsl module for PHP'
	install -D -m755 ${srcdir}/build-php/modules/xsl.so ${pkgdir}${PHPENV}/lib/php/modules/xsl.so
}
